apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

compileJava.options.encoding = "UTF-8"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

sourceSets {
    generated {
        java {
            srcDirs "${buildDir}/generated/source/proto/main/javalite", "${buildDir}/generated/source/proto/main/grpc"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    if (isReleaseMode) {
        api "com.skt.nugu.sdk:nugu-interface:$version"
        implementation "com.skt.nugu.sdk:nugu-core:$version"
    } else {
        api project(':nugu-interface')
        implementation project(':nugu-core')
    }

    api project(':nugu-agent')

    implementation "io.grpc:grpc-protobuf-lite:${protobuf_version}"
    implementation "io.grpc:grpc-stub:${grpc_version}"
    implementation "io.grpc:grpc-okhttp:${grpc_version}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protoc_version" }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}" }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
            task.builtins {
                remove java
            }
        }
    }
}

apply from: '../javadoc.gradle'
apply from: '../publish.gradle'