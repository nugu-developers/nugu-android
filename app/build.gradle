apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.skt.nugu.sampleapp"
        minSdkVersion 21
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name
            def fileNamePostfix = "OpenSDK-NuguSample-${buildType}-${variant.versionName}"
            outputFileName = "${fileNamePostfix}.apk"


            if (variant.getBuildType().isMinifyEnabled()) {
                variant.assemble.doLast {
                    copy {
                        from variant.mappingFile
                        into "${rootDir}/app/build/outputs/mapping/${flavor}/${buildType}"
                        rename { String fileName ->
                            "mapping_${fileNamePostfix}.txt"
                        }
                    }
                }
            }
        }
    }

    buildFeatures {
        compose true
        dataBinding true
    }

    composeOptions {
        kotlinCompilerVersion "1.5.10"
        kotlinCompilerExtensionVersion "1.0.0-rc02"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress "raw"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    if (isReleaseMode) {
        implementation "com.skt.nugu.sdk:nugu-android-helper:$version"
        implementation "com.skt.nugu.sdk:nugu-ux-kit:$version"
        implementation "com.skt.nugu.sdk:nugu-login-kit:$version"
        implementation "com.skt.nugu.sdk:nugu-service-kit:$version"
//        implementation "com.skt.nugu.sdk:nugu-client-kit:$version"
    } else {
        implementation project(':nugu-android-helper')
        implementation project(':nugu-ux-kit')
        implementation project(':nugu-login-kit')
        implementation project(':nugu-service-kit')
//        implementation project(':nugu-client-kit')
    }

    // include default resources
    implementation "com.skt.nugu.jademarble:default-resource:$jademarbleVersion"
    implementation "com.skt.nugu.keensense:default-resource:$keensenseVersion"

    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoPlayerVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidXAppcompatVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidXJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidXEspressoCoreVersion"

    implementation "com.google.android.material:material:$androidMaterialVersion"
    // 2.8.0 requires androidx.
    //noinspection GradleDependency

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinxCoroutinesAndroidVersion"

    // Koin for Kotlin
    implementation "io.insert-koin:koin-core:$koinVersion"
    implementation "io.insert-koin:koin-android:$koinVersion"
    implementation "io.insert-koin:koin-android-viewmodel:$koinVersion"

    // firebase distribution
    implementation platform('com.google.firebase:firebase-bom:26.4.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'androidx.preference:preference:1.1.1'

    // jetpack compose
    implementation 'androidx.compose.ui:ui:1.1.0-alpha01'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.0-alpha01'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.1.0-alpha01'
    // Material Design
    implementation 'androidx.compose.material:material:1.1.0-alpha01'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.1.0-alpha01'
    implementation 'androidx.compose.material:material-icons-extended:1.1.0-alpha01'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    // Integration with observables
    implementation 'androidx.compose.runtime:runtime-livedata:1.1.0-alpha01'
    implementation 'androidx.compose.runtime:runtime-rxjava2:1.1.0-alpha01'

    implementation "androidx.compose.compiler:compiler:1.1.0-alpha01"

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.0-alpha01'
}

sonarqube {
    skipProject = true
}

